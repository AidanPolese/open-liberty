*ASM CICS(NOPROLOG NOEPILOG SYSEIB)
*/*START OF SPECIFICATIONS ********************************************
*
* DESCRIPTIVE-NAME: WAS z/OS Adapters CICS Inquire current program
*
* CSECT NAME: BBOAIC42
*
* COMPONENT:  boss_adaptor
*
* EYE-CATCHER: none
*
* PROPRIETARY STATEMENT=
* IBM Confidential
* OCO Source Materials
* 5655-I35 (C) Copyright IBM Corp. 2014
* The source code for this program is not published or otherwise
* divested of its trade secrets, irrespective of what has been
* deposited with the U.S. Copyright Office.
* Status = H28W700
*
*
* FUNCTION:  Inquire the current CICS program
*
* METHOD-OF-ACCESS:
*
* SIZE:
*
* SERIALIZATION: None
*
* DEPENDENCIES: None
*
* EXTERNAL CLASSIFICATION: NONE
* END OF EXTERNAL CLASSIFICATION:
*
* CHANGE-ACTIVITY:
*
*   Flag Reason      Release  YYYYMMDD Origin  Description
*   $PI24444         H28W800  20140820  PDFG   Support CICS TS5.2
*
*END OF SPECIFICATIONS ***********************************************/
*
         TITLE 'BBOAIC42 - WAS Adapters CICS Inquire Current'
         DFHREGS ,
BBOAIC42 AMODE 31
BBOAIC42 RMODE ANY
         DFHUEXIT TYPE=XPIENV
BBOAIC42 CSECT
*----------------------------------------------------------------------
* On entry, R3  -> CICS Kernel Stack
*           R13 -> 72 byte register save area (RSA) with NAB
*           R14 -> Return-to address
*           R15 -> Entry point
*----------------------------------------------------------------------
         J     Around
         DC    CL8'BBOAIC42'            Module name
         DC    CL8' *OLA3* '            Stub marker - OLA3
         DC    CL10'&SYSDATE '          Asm date
         DC    CL6'&SYSTIME'            Asm time
Around   DS    0H
         STM   R14,R12,12(R13)  SAVE REGISTERS
         LR    R12,R15          SET BASE REGISTER
         USING BBOAIC42,R12
         L     R9,76(R13)       Load dynamic area into R9
         USING DYNAREA,R9
         LA    R8,PGISARGS      Load XPI parm list into R8
         USING DFHPGIS_ARG,R8   Address parm list
         LR    R4,R13           Save RSA address
         LR    R13,R3           Get the kernel stack into R13 (for XPI)
         DFHPGISX CALL,                                                *
               CLEAR,                                                  *
               IN,                                                     *
               FUNCTION(INQUIRE_CURRENT_PROGRAM),                      *
               OUT,                                                    *
               CURRENT_PROGRAM_NAME(CURRPROG),                         *
               INVOKING_PROGRAM_NAME(INVKPROG),                        *
               RESPONSE(*),                                            *
               REASON(*)
* TODO: The code above (XPI call) was moved here from bboatrue.  The
*       return code was never checked for validity.  We need to add
*       a return code check at some point.  For now, a pointer to the
*       current program name is returned in R15 and a pointer to the
*       invoking program name is returned in R0.
         LR    R13,R4            Restore RSA address in R13
         LA    R15,CURRPROG
         LA    R0,INVKPROG
         DROP  R9
         DROP  R8
DONE     DS    0H
         L     R14,12(R13)      Restore caller's R14
         LM    R1,R12,24(R13)   RESTORE CALLERS R1-R12
         BSM   0,R14            RETURN TO CALLER
*
         LTORG
*
* Dynamic area mapping
DYNAREA  DSECT
PGISARGS DS    CL(DFHPGIS_LEN)  Parm area for CICS call
         DS    0F               Align to word boundary
CURRPROG DS    CL8              Current pgm name
INVKPROG DS    CL8              Invoking pgm name
*
* DFHPGISA Mapping (for inquire current program name)
         COPY DFHPGISA
*
*
         END  BBOAIC42
