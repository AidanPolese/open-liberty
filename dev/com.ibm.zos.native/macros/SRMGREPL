*
* IBM Confidential
*
* OCO Source Materials
*
* Copyright IBM Corp. 2012
*
* The source code for this program is not published or otherwise
* divested of its trade secrets, irrespective of what has been
* deposited with the U.S. Copyright Office.
*
         MACRO
&NAME    SRMGREPL
         GBLC  &CCN_LITN
         GBLC  &CCN_CSECT
         GBLB  &BBGZ_SRMGREPL_DSECT
*
* This epilog gets control for RESMGR recovery in 64 bit mode.  It
* must return control to RESMGR in 31 bit mode, since that's the
* mode the prolog got control in.  So this un-does everything done
* by the prolog.
*
* Note that right now the prolog only supports running when there was
* a server task data control block to use.  So the epilog assumes
* that there is one.  When the proglog supports creating its own
* dynamic area, this code will need to be updated.
*
         LG 15,128(13) Load previous register save area (31 bit ptr)
         AGHI 13,-16   Subtract storage used for parameter list
         AGHI 13,-64   Subtract high halves of registers storage
         LGR  3,13     Copy save area addr to R3
         NILH  3,X'FFFE'   Subtract 64K prefix from dynamic area
         LGHI 6,-1 Load non-zero into R6 (expected value)
         LGHI 7,0  Load zero into R7
         CS   6,7,36(3) Try to return the dynamic area
         BRZ  RTNTOCALLER&SYSNDX Branch if we returned the dyn area
         LARL  5,&CCN_LITN
         USING &CCN_LITN,5
         ABEND ABEND_TYPE_SERVER,REASON=KRSN_SRMGREPL_STACK_IN_USE,    X
               DUMP,STEP,SYSTEM
         DROP  5
RTNTOCALLER&SYSNDX DS 0H
         LMH  14,12,0(13) Restore high halves of regs
         SAM31                 Switch to 31 bit mode
         SYSSTATE PUSH
         SYSSTATE AMODE64=NO   
         LR   13,15       Restore register save area
         LM   14,12,12(13)     Restore registers
         LHI  15,0             Always RC=0
         BR   14               Return to caller
         SYSSTATE POP
*
* Define the DSECTs we're going to use if this is the first time the
* macro is being invoked.
*
         AIF (&BBGZ_SRMGREPL_DSECT).NODSECT
&BBGZ_SRMGREPL_DSECT SETB 1
*
&CCN_CSECT CSECT
.NODSECT ANOP
         MEND
         