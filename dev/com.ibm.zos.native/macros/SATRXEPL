*
* IBM Confidential
*
* OCO Source Materials
*
* Copyright IBM Corp. 2011
*
* The source code for this program is not published or otherwise
* divested of its trade secrets, irrespective of what has been
* deposited with the U.S. Copyright Office.
*
         MACRO
&NAME    SATRXEPL
         GBLC  &CCN_LITN
         GBLC  &CCN_CSECT
         GBLB  &BBGZ_SATRXEPL_DSECT
*
* This epilog gets control for RRS exit routines in 64 bit mode.  It
* must return control to RRS in 31 bit mode, since that's the
* mode the prolog got control in.  So this un-does everything done
* by the prolog.
*
         LG   2,128(13)        Load previous register save area
         AGHI 13,-80           Subtract param list and temp storage
         USING SATRXEPL_TEMP,13
         SAM31
         SYSSTATE PUSH
         SYSSTATE AMODE64=NO   
         LMH  14,12,SATRXEPL_TEMP_HHREGS Restore high halves of regs
*
* Release the dynamic area
*         
         LARL 15,&CCN_LITN     Address constants
         USING &CCN_LITN,15
         LR   3,13             Storage release can't use R13
         STORAGE RELEASE,LENGTH=SATRXEPL_TEMP_STORAGE_LEN,ADDR=(3),    X
               SP=SATRXEPL_TEMP_STORAGE_SUBPOOL,                       X
               KEY=SATRXEPL_TEMP_STORAGE_KEY
         DROP 15
         DROP 13
*
* Restore the registers and return
*
         LR   13,2             Load previous register save area
         LM   14,12,12(13)     Restore registers
         BR   14               Return to caller
         SYSSTATE POP
*
* Define the DSECTs we're going to use if this is the first time the
* macro is being invoked.
*
         AIF (&BBGZ_SATRXEPL_DSECT).NODSECT
&BBGZ_SATRXEPL_DSECT SETB 1
SATRXEPL_TEMP            DSECT    Mapping of temporary storage
SATRXEPL_TEMP_HHREGS     DS CL60  High halves of R14-R12
SATRXEPL_TEMP_CLEAR      DS CL64  Cleared storage
                         ORG SATRXEPL_TEMP_CLEAR
SATRXEPL_TEMP_STG        DS F     Storage obtain origin
SATRXEPL_TEMP_PARM       DS FD    Addr of RRS exit parm list
SATRXEPL_TEMP_AVAIL      DS CL8   Available
SATRXEPL_TEMP_DYNA       DS CL32688 Dynamic area
*
SATRXEPL_TEMP_STORAGE_LEN EQU 32768   Length of temporary storage
SATRXEPL_TEMP_STORAGE_SUBPOOL EQU 249 Subpool of temporary storage
SATRXEPL_TEMP_STORAGE_KEY EQU 2       Key of temporary storage
*
&CCN_CSECT CSECT
.NODSECT ANOP
         MEND