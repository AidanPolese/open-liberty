/*******************************************************************************
 * Copyright (c) 2017 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     IBM Corporation - initial API and implementation
 *******************************************************************************/
/*
 * Master Gradle build script
 *
 * Depends on bndPlugin property set by settings.gradle.
 * and bnd_* values from gradle.properties.
 */

import aQute.bnd.build.Workspace
import aQute.bnd.osgi.Constants

/* Add bnd gradle plugin as a script dependency */
buildscript {
  dependencies {
    classpath bndPlugin
  }
}

/* Initialize the bnd workspace */
Workspace.setDriver(Constants.BNDDRIVER_GRADLE)
Workspace.addGestalt(Constants.GESTALT_BATCH, null)
ext.bndWorkspace = new Workspace(rootDir, bnd_cnf).setOffline(gradle.startParameter.offline)
ext.cnf = rootProject.project(bnd_cnf)

/* Set extra properties extension using generated.properties
 * generated.properties is tailored at Gradle initialization in settings.gradle
 */
Properties props = new Properties()
file("${rootDir}/generated.properties").withInputStream { props.load(it) }

ext.branchName = props.getProperty("version.qualifier")
ext.matrixParams = props.getProperty("artifactory.matrix.parameters")
ext.isAutomatedBuild = parseBoolean(props.getProperty("is.automated.build"))
ext.isContinuousBuild = parseBoolean(props.getProperty("is.continuous.build"))
ext.isRelease = parseBoolean(props.getProperty('is.release'))
ext.isRemotePublishing = parseBoolean(props.getProperty('is.remote.publishing'))
ext.reallyPublishBintray = parseBoolean(props.getProperty('really.publish.bintray'))

// Test results for this run
ext.testCountTotal = 0
ext.successfulTestCountTotal = 0
ext.failedTestCountTotal = 0
ext.skippedTestCountTotal = 0

private boolean parseBoolean(String value) {
    return 'on'.equalsIgnoreCase(value) || 'true'.equalsIgnoreCase(value)
}

/* Configure the subprojects */
subprojects {
  def bndProject = bndWorkspace.getProject(name)
  if (bndProject != null) {
    plugins.apply 'biz.aQute.bnd'
  }
  /* For Java 9 builds, set the --release compiler flag to keep bootclasspath in sync */
  compileJava {
    doFirst {
      if(JavaVersion.current().isJava9Compatible()) {
        def release = sourceCompatibility
        if(sourceCompatibility < '9')
          release = sourceCompatibility.substring(2);
		println "Compiling with --release=" + release
        options.compilerArgs.addAll([ '--release', release])
      }
    }
  }
}

task wrapper(type: Wrapper) {
  jarFile = rootProject.file('.gradle-wrapper/gradle-wrapper.jar')
}

apply from: 'cnf/gradle/scripts/tasks.gradle'