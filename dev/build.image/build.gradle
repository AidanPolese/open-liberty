
import org.apache.tools.ant.filters.ReplaceTokens

// Set the generated zip version to the buildID
// I am not sure why the tasks.gradle file where this task is
// defined can't see the bnd property, but it can't and this
// at least works (even if it feels hacky)
parent.zipOpenLiberty.setProperty("version", bnd.buildID)

task copyPropertiesToBuildImage (type:Copy) {
    dependsOn jar
    from "${projectDir}/publish/gradle"
    into "${projectDir}/wlp/lib/versions"
    include '*.properties'
    filter(ReplaceTokens,
           tokens: [PRODUCT_VERSION: bnd.libertyRelease, PRODUCT_EDITION: bnd.productEdition, PRODUCT_LICENSE_TYPE: bnd.productLicenseType])
}

task copyReadmeToBuildImage (type:Copy) {
    dependsOn jar
    from "${projectDir}/publish/gradle"
    into "${rootDir}/build.image/wlp"
    include 'README.TXT'
    filter(ReplaceTokens,
           tokens: [BUILD_ID: bnd.buildID, LIBERTY_VERSION: bnd.libertyRelease])
}
/*
task copyCopyrightToBuildImage (type:Copy) {
    dependsOn jar
    from "${projectDir}"
    into "${rootDir}/build.image/wlp"
    include 'Copyright.txt'
}
*/
task copyGeneratedToBuildImageBinTools (type:Copy) {
    dependsOn jar
    from "${projectDir}/wlp/lib"
    into "${projectDir}/wlp/bin/tools"
    include 'bootstrap-agent.jar'
    rename 'bootstrap-agent.jar', 'ws-javaagent.jar'
}

task copyGeneratedToCnfLib (type:Copy) {
    dependsOn jar
    from "${projectDir}/wlp/dev/api/spec/"
    into "${rootDir}/${bnd_cnf}/lib"
    include 'com.ibm.ws.org.osgi.core.6.0.0_*.jar'
    rename '.*.jar', 'osgi.core.jar'
}

assemble {
    dependsOn copyPropertiesToBuildImage
    dependsOn copyReadmeToBuildImage
    //dependsOn copyCopyrightToBuildImage
    dependsOn copyGeneratedToBuildImageBinTools
    dependsOn copyGeneratedToCnfLib
}

clean.doLast {
    file('wlp').deleteDir()
}

publishing {
  publications {
    maven(MavenPublication) {
      artifactId 'openliberty'
      version project.version
      artifact parent.zipOpenLiberty
    }
  }
}
